// sudoku-logic.andl -- sudoku solver logic only, no guessing
// 

// fixed data structures
digits := {{ sdigit:= '1', ndigit := 1}} 
  .while( {{ sdigit:=text(ndigit+1), ndigit:=ndigit+1 }} 
    .where(ndigit <= 9) )
digitsx := digits union {{ sdigit := '.', ndigit := 0 }}
units := {{ index := 0, row := 0, col := 0, box := 0 }} 
  .while({{ index := index + 1, 
          row := (index + 1) div 9, 
          col := (index + 1) mod 9, 
          box := (index + 1) div 3 mod 3 + (index + 1) div 27 * 3 }} .where(index <= 80) )
poss := units .select{ index } join digits .select{ ndigit }
possu := units join digits .select{ ndigit }

// useful functions
showb(t:text) => do {
    seq(11) .select{ N, line:= 
        if(N mod 4 = 3, 
           fill('-', 9), 
           right(left(t, 9 + (N - N div 4) * 9), 9))}
}

// Show a set of knowns. First fill out all index values, then convert to text
showunk(k:poss) => do {
    t := (k union (units ajoin k)
      .select{ index, ndigit := 0}) join digitsx 
      .select{ ndigit, sdigit }
    showb(t .order(index) .select{ fold(&, sdigit) })
}

// original raw data
inp := {{ sud := '53..7....6..195....98....6.8...6...34..8.3..17...2...6.6....28....419..5....8..79' }}
//inp := {{ sud := '1....7.9..3..2...8..96..5....53..9...1..8...26....4...3......1..4......7..7...3..' }}
inp
board := ((units join inp) 
  .select{ * sud, sdigit := right(left(sud, index + 1), 1) } compose digitsx) 
  .select{ index, ndigit }
knowns := board .where( ndigit <> 0)
'Knowns=' & knowns.count
showunk(knowns)

// algorithm
// The solver, .whiles as long as it can make progress. After this need to guess.
solution := knowns .while( 
    do {
        // start with the 729 possiblities, progressively remove conflicts with knowns
        knownsu := knowns join units
        allowedu := possu ajoin knownsu .select{ index } ajoin 
	            knownsu .select{ row, ndigit } ajoin 
		    knownsu .select{ col, ndigit } ajoin 
		    knownsu .select{ box, ndigit } 

        // algorithm 1 - a cell with only one possible digit must be that digit
        new1 := allowedu .select{ index, tot:=fold(+,1) } 
	  .where(tot=1) join allowedu

        // algorithm 2 - a digit with only one place in a unit must go there
        new2a := allowedu .select{ ndigit, row, tot:=fold(+,1) } .where(tot=1) join allowedu
        new2b := allowedu .select{ ndigit, col, tot:=fold(+,1) } .where(tot=1) join allowedu
        new2c := allowedu .select{ ndigit, box, tot:=fold(+,1) } .where(tot=1) join allowedu

        new1 .select{ index, ndigit } union new2a union new2b union new2c
    }
)
showunk(solution)
