// WebSpApiSetup.andl
// 
#catalog new update

'WebSpApiSetup.andl -- set up for Web API SP sample'

// load initial data
var S:{S#,SNAME,STATUS:number,CITY}(csv)
var P:{P#,PNAME,COLOR,WEIGHT:number,CITY}(csv)
var SP:{S#,P#,QTY:number}(csv)

// Assume these will be accessed directly
supplier := S .select{ *Sid:=S# }
part := P .select{ *Pid := P# }
supplies := SP .select{ *Sid := S#, Pid:= P# }

// CRUD interface for supplier table

def find_supplier(sid:text) => do {
    supplier .where(Sid = sid)
}

def findall_supplier() => do {
    supplier .order(Sid)
}

def create_supplier(s:supplier) => do {
    update supplier union s
}

def delete_supplier(sid:text) => do {
    update supplier .where(Sid = sid) .select{}
}

def update_supplier(sid:text, s:supplier) => do {
    update supplier .where(Sid = sid) .select{}
    update supplier union s
}

//--- part

def findall_part() => part

def find_part_by_name(name:text) => do {
    part .where(PNAME =~ name)
}

tquery := {{ Key:text,Value:text }}
def findsome_part(query:tquery) => do {
    pattern := query .where(Key = 'PNAME') .select{ (Value) }
    part .where(PNAME =~ pattern)
}

findall_supplies => supplies

andl_operator()
