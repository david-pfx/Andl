// WebSpRestSetup.andl
// 
#catalog new update

'WebSpRestSetup.andl -- set up for Web REST SP sample'

// load initial data
var S(csv), P(csv), SP(csv)

supplier := S .select{ *Sid:=S# }
part := P .select{ *Pid := P# }
supplies := SP .select{ *Sid := S#, Pid:= P# }

// CRUD interface for supplier table

get_supplier_id(sid:text) => do {
    supplier .where(Sid = sid)
}

get_supplier() => do {
    supplier .order(Sid)
}

add_supplier(s:supplier) => do {
    update supplier union s
}

delete_supplier_id(sid:text) => do {
    update supplier .where(Sid = sid) .select{}
}

put_supplier_id(sid:text, s:supplier) => do {
    update supplier .where(Sid = sid) .select{}
    update supplier union s
}

get_part() => part

get_part_q(query:{{ Key:text,Value:text }}) => do {
    pattern := query .where(Key = 'PNAME') .select{ (Value) }
    part .where(PNAME =~ pattern)
}

get_supplies => supplies

andl_catalog
//andl_operator()
