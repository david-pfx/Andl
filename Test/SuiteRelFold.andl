// Test Suite -- testing of aggregation

#catalog

start($filename$)

var S(csv), P(csv), SP(csv)

// simple folds
test_true($lineno$, 30 = S .select{ fold(max,STATUS) })
test_true($lineno$, 10 = S .select{ fold(min,STATUS) })
test_true($lineno$, false = S .select{ fold(and,STATUS = 10) })
test_true($lineno$, true = S .select{ fold(and,STATUS >= 10) })
test_true($lineno$, true = S .select{ fold(or,STATUS = 10) })
test_true($lineno$, false = S .select{ fold(or,STATUS < 10) })

test_true($lineno$, 20+10+30+20+30 = S .select{ fold(+,STATUS) })
test_true($lineno$, 20*10*30*20*30 = S .select{ fold(*,STATUS) })

// will not compile - cannot invoke overloaded operator
//test_true($lineno$, 0 and 20 and 10 and 30 and 20 and 30 = S .select{ fold(and,STATUS) })
//test_true($lineno$, 0 or 20 or 10 or 30 or 20 or 30 = S .select{ fold(or,STATUS) })
//test_true($lineno$, 20&10&30&20&30 = S .order(S#) .select{ fold(&,STATUS) })

// this way works
bitand(a:0,b:0) => a and b
bitor(a:0,b:0) => a or b
test_true($lineno$, (0 and 20 and 10 and 30 and 20 and 30) = S .select{ fold(bitand,STATUS) })
test_true($lineno$, (0 or 20 or 10 or 30 or 20 or 30) = S .select{ fold(bitor,STATUS) })
test_true($lineno$, 20&10&30&20&30 = S .order(S#) .select{ fold(&,STATUS.text) })

// more operators
test_true($lineno$, 1/20/10/30/20/30 = S .order(S#) .select{ fold(/,STATUS) })
test_true($lineno$, 0-20-10-30-20-30 = S .order(S#) .select{ fold(-,STATUS) })

// above and below fold
test_true($lineno$, 110 = S .select{ fold(+,STATUS) })
test_true($lineno$, 115 = S .select{ fold(+,STATUS+1) })
test_true($lineno$, 111 = S .select{ fold(+,STATUS)+1 })
test_true($lineno$, 111 = S .select{ fold(+,STATUS) } +1)

// folded bare function
fmax(n:0) => fold(max,n)
fcat(s:'') => fold(&,s)
test_true($lineno$, 30 = S .select{ fmax(STATUS)  })
test_true($lineno$, 20&10&30&20&30 = S .order(S#) .select{ fcat(STATUS.text)  })

// folded do function
fdomax(n:0) => do { fold(max,n) }
fdocat(s:'') => do { fold(&,s) }
test_true($lineno$, 30 = S .select{ fdomax(STATUS) })
test_true($lineno$, 20&10&30&20&30 = S .order(S#) .select{ fdocat(STATUS.text) })

// folded do multi-line function
fdomax2(n:0) => do { 
    x:=n*10
    fold(max,x) 
}
fdocat2(s:'') => do { 
    ss:=s & 'x'
    fold(&, ss) 
}
test_true($lineno$, 300 = S .select{ fdomax2(STATUS) })
test_true($lineno$, '20x10x30x20x30x' = S .order(S#) .select{ fdocat2(STATUS.text) })

// These previously placed in catalog
//fold_sum(n:0) => fold(+,n)
//fold_ave(n:0) => fold(+,n)/fold(+,1)
test_true($lineno$, 110 = S .select{ fold_sum(STATUS)  })
test_true($lineno$, 22 = S .select{ fold_ave(STATUS)  })

feven:0(n1:0, n2:0) => if(n2 mod 20 = 0, n1+n2, n1-n2)
fcomma(s1:'', s2:'') => if(s1='',s2, s1 & ', ' & s2)
test_true($lineno$, -30 = S .select{ fold(feven,STATUS) })
test_true($lineno$, '20, 10, 30, 20, 30' = S .order(S#) .select{ fold(fcomma,STATUS.text) })

PX := P .select{ * W1:=WEIGHT, W2:=WEIGHT*2-10 }

R1 := PX .select{ COLOR, SW1:=fold(+,W1), SW2 := fold(+,W2), SSW1:=fold(+,W1*W1), SSW2 := fold(+,W2*W2), AW1 := fold(+,W1)/fold(+,1), AW2 := fold(+,W2)/fold(+,1) } 
sum(n:0) => fold(+,n)
sumsq(n:0) => fold(+,n*n)
avg(n:0) => fold(+,n)/fold(+,1)
R2 := PX .select{ COLOR, SW1:=sum(W1), SW2 := sum(W2), SSW1:=sumsq(W1), SSW2 := sumsq(W2), AW1 := avg(W1), AW2 := avg(W2) } 
test_true($lineno$, R1 = R2)

finish($filename$)
