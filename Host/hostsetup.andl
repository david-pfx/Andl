// hostsetup.andl -- set up for access by Web Host (WCF)
// 
#catalog new update

'webhostspsetup.andl -- set up for access by Web Host REST API'

// load initial data
var S:{S#,SNAME,STATUS:number,CITY}(csv)
var P:{P#,PNAME,COLOR,WEIGHT:number,CITY}(csv)
var SP:{S#,P#,QTY:number}(csv)

supplier := S .select{ *Sid:=S# }
part := P .select{ *Pid := P# }
supplies := SP .select{ *Sid := S#, Pid:= P# }

// CRUD interface for supplier table

def get_supplier_id(sid:text) => do {
    supplier .where(Sid = sid)
}

def get_supplier() => do {
    supplier .order(Sid)
}

def add_supplier(s:supplier) => do {
    update supplier union s
}

def delete_supplier_id(sid:text) => do {
    update supplier .where(Sid = sid) .select{}
}

def put_supplier_id(sid:text, s:supplier) => do {
    update supplier .where(Sid = sid) .select{}
    update supplier union s
}

def get_part() => part

def get_part_q(query:{{ Key:text,Value:text }}) => do {
    pattern := query .where(Key = 'PNAME') .select{ (Value) }
    part .where(PNAME =~ pattern)
}

get_supplies => supplies

andl_catalog
//andl_operator()
