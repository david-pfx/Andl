// thriftsupplierpart.andl -- set up for access by Thrift API
// 

// Run setup.bat to do some cleanup and these two steps.
// 1. Compile this with -t and output to <sp> database
//      >Andl.exe ThriftSupplierPart.andl sp -t
// 2. Compile generated interface with Thrift and csharp output
//      >Thrift --gen csharp ThriftSupplierPart.thrift

// Then do this.
// 3. Build the ThriftSupplierPart client: ThriftSupplierPart (compiles and links to generated code)
// 4. Run the Thrift server (Ctrl+C to shut down): 
//      >Andl.Thrift.exe sp
// 5. From another session, run the ThriftSupplierPart client
//      >ThriftSupplierPart.exe

#catalog new update

'thriftsupplierpart.andl -- set up for access by Thrift API'

// load initial data
var S(csv), P(csv), SP(csv)

Supplier := S .select{ *Sid:=S# }
Part := P .select{ *Pid := P# }
Supplies := SP .select{ *Sid := S#, Pid:= P# }
Tquery := {{ Key:text,Value:text }}

// CRUD interface for supplier table

find_supplier(sid:text) => do {
    Supplier .where(Sid = sid)
}

findall_supplier() => do {
    Supplier .order(Sid)
}

create_supplier(s:Supplier) => do {
    update Supplier union s
}

delete_supplier(sid:text) => do {
    update Supplier .where(Sid = sid) .select{}
}

update_supplier(sid:text, s:Supplier) => do {
    update Supplier .where(Sid = sid) .select{}
    update Supplier union s
}

findall_part() => Part

findsome_part(query:Tquery) => do {
    pattern := query .where(Key = 'PNAME') .select{ (Value) }
    Part .where(PNAME =~ pattern)
}

findall_supplies => Supplies
findsome_part({{ Key := 'PNAME', Value := 'S.*' }})

andl_catalog
//andl_operator()
